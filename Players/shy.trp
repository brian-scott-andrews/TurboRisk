{
Program: Shy

Author:  Julio Couce Molina (jcm790@hotmail.com)

Description: It slowly conquers territories, trying to keep its frontiers
fortified and trying to complete continents. 
Works better with many players. No borg-style cooperation here. This one 
tries to begin at South America. If you'd prefer it to begin
at a random place, try Shy2.
}
(*

Assigment: It prefers South America. If SA is all taken, it selects a territory randomly.

Placement: Tries to avoid having frontier territories with less than 25 armies, beginning with those frontier territories with less armies. If all frontier territories have at least 25 armies, it selects the territory with more armies.

Attack: It only attacks with a 2 to 1 ratio or better. It prefers to attack territories in the same continent until it conquers that continent, but it can attack territories in other countries if that other continent is controlled by one player or if the PlayesArmies/EnemyArmies ratio is very good.

Occupation: If From and To territories are frontiers, half the armies go to each territory. If only one of them is frontier, all armies (except one) go to that territory. If none of them is frontier, all armies (except one) go to To territory.

Fortification: Selects non-frontier territory with more armies, and moves them to a neighbouring frontier territory. If there are not neighbouring frontier territories, it moves the armies to a random neighbouring territory.

Thanks: Thanks to Mario Ferrari, for creating this wonderful game (Turborisk) and making it freeware. I especially like that you can program your own artificial intelligence. It's fun, and that way you can't complain about how stupid the AI is, because, if you think that, why don't you program your own?

*)


procedure Assignment(var ToTerritory: integer);
var
  T,VarAuxi: integer;
begin
  // territorio libre al azar
  ToTerritory:=0;
  T:=trunc(URandom(42)+1);
  VarAuxi:=0;
  repeat
    if TOwner(T)=0 then
      ToTerritory:=T;
    T:=T+1;
    if (T>42) and (VarAuxi=0) then 
    begin
       T:=1;
       VarAuxi:=1;
    end;
  until (ToTerritory>0) or (T>42);
  //pero con preferencia a S. América
  if TOwner(12)=0 then ToTerritory:=12;
  if TOwner(10)=0 then ToTerritory:=10;
  if TOwner(11)=0 then ToTerritory:=11;
  if TOwner(13)=0 then ToTerritory:=13;
end;




procedure Placement(var ToTerritory: integer);
var
  Contador,Auxiliar,TInicial,T,VarAuxi,MaxArmies,FCount,F: integer;
begin
  // Coloca en territorio frontera con menos de 25 ejércitos
  // (de entre ellos escoge el más cercano a 25),
  // o si no en el territorio con más ejércitos 

  // busca territorio frontera con menos de 25
  ToTerritory:=0;
  MaxArmies:=100;
  T:=trunc(URandom(42)+1);
  TInicial:=T;
  VarAuxi:=0;
  repeat
    if (TIsFront(T)) and (TArmies(T)<MaxArmies) and (TArmies(T)<25) then
    begin
      ToTerritory:=T;
      MaxArmies:=TArmies(T);
    end;
    T:=T+1;
    if (T>42) and (VarAuxi=0) then 
    begin
       T:=1;
       VarAuxi:=1;
    end;
  until (VarAuxi=1) and (T>=TInicial);

  Auxiliar:=0;
  if (ToTerritory<>0) then begin
    if (TArmies(ToTerritory)=1) then begin
      FCount:=TBordersCount(ToTerritory);
      for F:=1 to FCount do begin
        If TIsMine(TBorder(ToTerritory,F)) then Auxiliar:=1;
      end;
        if (Auxiliar=0) then ToTerritory:=0;
    end;
  end;


  // si sólo hay un territorio con más de un ejército, debe ir a ese territorio
  Contador:=0;
  for T:=1 to 42 do begin
    if (TIsMine(T)) and (TArmies(T)>1) then Contador:=Contador+1;
  end;
  if (Contador=1) then ToTerritory:=0;
  if (Contador=0) and (TOwner(12)=PMe) then ToTerritory:=12;
  if (Contador=0) and (TOwner(10)=PMe) then ToTerritory:=10;
  if (Contador=0) and (TOwner(11)=PMe) then ToTerritory:=11;
  if (Contador=0) and (TOwner(13)=PMe) then ToTerritory:=13;



  // si no lo encuentra, busca territorio con más ejércitos
  if (ToTerritory=0) then begin
    MaxArmies:=0;
    T:=trunc(URandom(42)+1);
    TInicial:=T;
    VarAuxi:=0;
    repeat
      if (TIsMine(T)) and (TArmies(T)>MaxArmies) then
      begin
        ToTerritory:=T;
        MaxArmies:=TArmies(T);
      end;
      T:=T+1;
      if (T>42) and (VarAuxi=0) then 
      begin
         T:=1;
         VarAuxi:=1;
      end;
    until (VarAuxi=1) and (T>=TInicial);
  end;


  //Quitamos modo de ataque total
  UBufferSet(1,0);
  UBufferSet(2,0);
end;






procedure Attack(var FromTerritory, ToTerritory: integer);
var
  Completo,T2,F,FCount,T,ET,EA: integer;
  MejorOpcion,temp: double;
begin
  // Ataca al vecino más débil con un territorio que tenga más de 25 ejércitos
  // siempre que le supere por 2 a 1. No entra en nuevos continentes hasta terminar
  // los anteriores

  FromTerritory:=0;
  ToTerritory:=0;

  // si ya estamos en guerra total para la conquista de
  // un territorio, seguimos con ello
  if (UBufferGet(1)<>0) and (TArmies(trunc(UBufferGet(1)))>1) then begin
    FromTerritory:=trunc(UBufferGet(1));
    ToTerritory:=trunc(UBufferGet(2));
  end else begin
    // en caso contrario, entre todos nuestros países buscamos la mejor
    // opción de ataque (debemos tener más de 25 ejércitos y superar por 2 a 1)
    // No se entra en otro continente hasta terminar los que tiene a medias
    MejorOpcion:=2;
    Completo:=1;
    for T:=1 to 42 do begin
      if (COwner(TContinent(T))=0) and (TIsMine(T)) then Completo:=0;
    end;
    
    for T:=1 to 42 do begin
      if (TIsFront(T)) then begin
        FCount:=TFrontsCount(T);
        for F:=1 to FCount do begin
          T2:=TFront(T,F);
          temp:=TArmies(T)/TArmies(T2);
          if (temp>9) or ((TArmies(T)>=25) or (FCount=1)) and (temp>MejorOpcion) and ((TContinent(T)=TContinent(T2)) or (Completo=1) or ((COwner(TContinent(T2))<>0) and (COwner(TContinent(T2))<>PMe)))
          then begin
            FromTerritory:=T;
            ToTerritory:=T2;
            UBufferSet(1,T);
            UBufferSet(2,T2);
            MejorOpcion:=TArmies(T)/TArmies(T2);
          end;
        end;
      end;
    end;
  end;  
end;






procedure Occupation(FromTerritory, ToTerritory: integer; var Armies: integer);
var
  FromIsFront, ToIsFront: boolean;
begin
  // el típico que reparte los ejércitos mitad y mitad si ambos son frentes,
  // y si no los deja todos en el frente.

  FromIsFront := TIsFront(FromTerritory);
  ToIsFront := TIsFront(ToTerritory);
  Armies:=0;

  // si ambos territorios son 'front'
  if  FromIsFront and ToIsFront then begin
    // reparte mitad y mitad
    Armies:=(TArmies(FromTerritory)-TArmies(ToTerritory)) div 2;

  // si el territorio origen es front
  end else if FromisFront then begin
    // Todos los ejércitos se quedan aquí, así que no hay nada que hacer

  // en otros casos
  end else begin
    // todos los ejércitos al territorio ocupado (excepto 1)
    Armies:=TArmies(FromTerritory)-1;
  end;
  //Quitamos modo de ataque total
  UBufferSet(1,0);
  UBufferSet(2,0);
end;




procedure Fortification(var FromTerritory, ToTerritory, Armies: integer);
var
  T,B,MaxArmy: integer;
begin
  // La típica que mueve ejércitos que no están en el frente al frente
  //

  FromTerritory:=0;
  ToTerritory:=0;
  Armies:=0;

  // busca el máximo número de ejercitos que no están en front
  MaxArmy := 1;
  for T:=1 to 42 do begin
    if TIsMine(T) and not TIsFront(T) then begin  // territorio mío y no front
      if TArmies(T)>MaxArmy then begin
        MaxArmy := TArmies(T);
        FromTerritory := T;
      end;
    end;
  end;
  // si hay ejércitos que mover...
  if FromTerritory>0 then begin
    // ver si se pueden mover a front...
    for B:=1 to TBordersCount(FromTerritory) do begin
      if ToTerritory=0 then begin
        T:=TBorder(FromTerritory,B);
        if TIsMine(T) and TIsFront(T) then begin
          ToTerritory:=T;
        end;
      end;
    end;
    // en otro caso se mueven al primer territorio vecino
    if ToTerritory=0 then begin
      repeat
        B:=trunc(1+URandom(TBordersCount(FromTerritory)));
        T:=TBorder(FromTerritory,B);
        if TIsMine(T) then begin
          ToTerritory:=T;
        end;
      until (ToTerritory>0);
    end;
    // Mover todos los ejércitos (excepto 1)
    if ToTerritory>0 then begin
      Armies:=TArmies(FromTerritory)-1;
    end;
  end;
end;

begin
end.