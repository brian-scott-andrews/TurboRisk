{
Program: Simple

Author: Mario Ferrari

Description: Purpose of this program is to demonstrate how to code a computer-player
in TurboRisk. As simplicity was the main goal, the resulting strategy is very weak.

}

// History: Fortification Procedure updated for TurboRisk 2.0 by Nathan Scarbrough August 2009


procedure Assignment(var ToTerritory: integer);
var
  T: integer;
begin
  // find first free territory (a very stupid way to start the game!)
  ToTerritory:=0;
  T:=1;
  repeat
    if TOwner(T)=0 then
      ToTerritory:=T;
    T:=T+1;
  until (ToTerritory>0) or (T>42);
end;

procedure Placement(var ToTerritory: integer);
var
  T, A,B: integer;
  Ratio,MaxRatio,NewRatio: double;
begin
  {$DEFINE PS_DELPHIDIV}
//  ULogOn;
  // reinforce territory with greatest ratio between total enemy armies and
  // own armies
  ToTerritory:=0;
  MaxRatio:=0;
  for T:=1 to 42 do begin
    if TIsFront(T) then begin  // territory is mine and front
      Ratio := 1.0*TPressure(T) / TArmies(T);
      A:=1;
      B:=2;
      NewRatio := A / B;
      ULog('Here');
      if NewRatio>0 then begin
         ULog('It is!');
      end;
      if Ratio>MaxRatio then begin
        ToTerritory:=T;
        MaxRatio:=Ratio;
      end;
    end;
  end;
end;

procedure Attack(var FromTerritory, ToTerritory: integer);
var
  T,EnemyT,EnemyA: integer;
  Ratio,MaxRatio: double;
begin
  // perform attack with best chances; stop after 1 conquest;
  FromTerritory:=0;
  ToTerritory:=0;
  if not SConquest then begin  // still no conquest
    MaxRatio:=0;
    for T:=1 to 42 do begin
      if TIsFront(T)                      // territory is mine and front
      and (TArmies(T)>1) then begin       // attack is possible
        TWeakestFront(T,EnemyT,EnemyA);
        Ratio := TArmies(T) / EnemyA;
        if Ratio>MaxRatio then begin
          MaxRatio:=Ratio;
          FromTerritory:=T;
          ToTerritory:=EnemyT;
        end;
      end;
    end;
  end;
end;

procedure Occupation(FromTerritory, ToTerritory: integer; var Armies: integer);
var
  FromIsFront, ToIsFront: boolean;
begin
  // very simple but effective routine

  FromIsFront := TIsFront(FromTerritory);
  ToIsFront := TIsFront(ToTerritory);
  Armies:=0;

  // if both territories are 'front'
  if  FromIsFront and ToIsFront then begin
    // balance armies
    Armies:=(TArmies(FromTerritory)-TArmies(ToTerritory)) div 2;

  // if "From" territory is front
  end else if FromisFront then begin
    // all armies stay in From territory, so there's nothing to do

  // in the other cases
  end else begin
    // all armies in the conquered territory (except one which must stay)
    Armies:=TArmies(FromTerritory)-1;
  end;
end;

procedure Fortification(var FromTerritory, ToTerritory, Armies: integer);
var
  T,MaxArmy: integer;
begin
  FromTerritory:=0;
  ToTerritory:=0;
  Armies:=0;

  // look for the greatest number of armies which are not on a front territory
  // and move them toward the nearest front
  MaxArmy:= 1;
  For T:= 1 to 42 do begin
    if TIsMine(T) and (not TIsFront(T)) then begin
      if (TArmies(T) > MaxArmy) then begin
        MaxArmy:= TArmies(T);
        FromTerritory:= T;
        ToTerritory:= TPathToFront(T);
        Armies:=TArmies(T)-1; // Move all armies to destination
      end;                    // (except the one which must stay)
    end;
  end;
end;

begin
end.

