NAPOLEON
--------

A TurboRisk program by Giulio Ferrari (giuliof@iol.it).

Notes:

A program that tries to conquer and defend continents. 
Works best against 2 or 3 other computer players.

Procedures:

ASSIGNMENT: same as simple.trp

PLACEMENT: place armies in the continent with best
ratio owned/total territories 

ATTACK: try to conquer continents;
quite aggressive: perform attacks if force ratio>=1.1

OCCUPATION: if both territories are 'front'
overcharge the conquered territory's armies
(to increase attack possibilities)

FORTIFICATION: same as simple.tlp

-----

procedure Assignment;
var
  T: integer;
begin
  // find first free territory (a very stupid way to start the game!)
  ToTerritory:=0;
  T:=1;
  repeat
    if TOwner(T)=0 then
      ToTerritory:=T;
    T:=T+1;
  until (ToTerritory>0) or (T>42);
end;

procedure Placement;
var
  T,EnemyT,EnemyA,BestC,C,
  PT,PA,ET,EA: integer;
  Ratio,MaxRatio,BestRatio: double;
begin
  // select best continent (best ratio owned/total territories,
  // except owned continents)
  BestC:=1;
  BestRatio:=0;
  for C := 1 to 6 do
  begin
    CAnalysis(C,PT,PA,ET,EA);
    if PT / CTerritoriesCount(C) < 1 then
    begin
      if PT / CTerritoriesCount(C) > BestRatio then begin
        BestRatio:= PT / CTerritoriesCount(C);
        BestC:=C;
      end;
    end;
  end;
  // reinforce the territory (in the continent if possible)
  // with greatest ratio between total enemy armies and
  // own armies
  ToTerritory:=0;
  MaxRatio:=0;
  for T:=1 to 42 do begin
    // territory is mine and front
    if ( (BestRatio=0) or ((BestRatio<>0) and (TContinent(T)=BestC)) )
    and TIsFront(T) then begin 
      Ratio := TPressure(T) / TArmies(T);
      if Ratio>MaxRatio then begin
        ToTerritory:=T;
        MaxRatio:=Ratio;
      end;
    end;
  end;
end;

procedure Attack;
var
  T,EnemyT,EnemyA,BestC,C,
  PT,PA,ET,EA,i: integer;
  Ratio,MaxRatio,BestRatio: double;
begin
  // select best continent (best ratio owned/total territories,
  // except owned continents)
  BestC:=1;
  BestRatio:=0;
  for C := 1 to 6 do
  begin
    CAnalysis(C,PT,PA,ET,EA);
    if PT / CTerritoriesCount(C) < 1 then
    begin
      if PT / CTerritoriesCount(C) > BestRatio then begin
        BestRatio:= PT / CTerritoriesCount(C);
        BestC:=C;
      end;
    end;
  end;
  // perform attack in the selected continent
  FromTerritory:=0;
  ToTerritory:=0;
  MaxRatio:=0;
  for T:=1 to 42 do
    if (TContinent(T)=BestC) and TIsFront(T) and (TArmies(T)>1) then begin
      for i:= 1 to TBordersCount(T) do begin
        if (not TIsMine(TBorder(T,i))) and (TContinent(TBorder(T,i))=BestC) then begin
          Ratio := TArmies(T) / TArmies(TBorder(T,i));
          if (Ratio>MaxRatio) then begin
            FromTerritory:=T;
            ToTerritory:=TBorder(T,i);
            MaxRatio:=Ratio;
          end;
        end;
      end;
    end;
  // if there's no continent to be completed, perform standard attack
  if MaxRatio=0 then begin
    MaxRatio:=0;
    for T:=1 to 42 do begin
      if TIsFront(T) and (TArmies(T)>1) then begin       
        TWeakestFront(T,EnemyT,EnemyA);
        Ratio := TArmies(T) / EnemyA;
        if Ratio>MaxRatio then begin
          FromTerritory:=T;
          ToTerritory:=EnemyT;
          MaxRatio:=Ratio;
        end;
      end;
    end;
  end;
  if MaxRatio<1.1 then    // suspend attacks when ratio too low
    FromTerritory:=0;
end;

procedure Occupation;
var
  FromIsFront, ToIsFront: boolean;
begin
  // very simple but effective routine

  FromIsFront := TIsFront(FromTerritory);
  ToIsFront := TIsFront(ToTerritory);
  Armies:=0;

  // if both territories are 'front'
  if  FromIsFront and ToIsFront then begin
    // overcharge the conquered territory's armies
    Armies:=(TArmies(FromTerritory)* 3 div 2-TArmies(ToTerritory)) div 2;
  
  // if "From" territory is front
  end else if FromisFront then begin
    // all armies stay in From territory, so there's nothing to do

  // in the other cases
  end else begin
    // all armies in the conquered territory (except one which must stay)
    Armies:=TArmies(FromTerritory)-1;
  end;
end;

procedure Fortification;
var
  T,B,MaxArmy: integer;
begin
  FromTerritory:=0;
  ToTerritory:=0;
  Armies:=0;

  // look for the greatest nuber of armies which is not on a front territory
  MaxArmy := 1;
  for T:=1 to 42 do begin
    if TIsMine(T) and not TIsFront(T) then begin  // territory is mine and not "front"
      if TArmies(T)>MaxArmy then begin
        MaxArmy := TArmies(T);
        FromTerritory := T;
      end;
    end;
  end;
  // if there are armies to move...
  if FromTerritory>0 then begin
    // check if can move armies to front...
    for B:=1 to TBordersCount(FromTerritory) do begin
      if ToTerritory=0 then begin
        T:=TBorder(FromTerritory,B);
        if TIsMine(T) and TIsFront(T) then begin
          ToTerritory:=T;
        end;
      end;
    end;
    // otherwise move armies in the first bordering territory
    if ToTerritory=0 then begin
      for B:=1 to TBordersCount(FromTerritory) do begin
        if ToTerritory=0 then begin
          T:=TBorder(FromTerritory,B);
          if TIsMine(T) then begin
            ToTerritory:=T;
          end;
        end;
      end;
    end;
    // Move all armies to destination (except the one which must stay)
    if ToTerritory>0 then begin
      Armies:=TArmies(FromTerritory)-1;
    end;
  end;
end;

